---
title: "30daysOS-第08天 鼠标控制 32位模式切换"
date: 2021-03-30T18:12:00+08:00
tags: [学习笔记, 学习, 笔记]
draft: false
description: "30天自制操作系统"
source: "http://hrb.osask.jp/"
---
#操作系统 #30daysOS 

# 解析鼠标

鼠标中断一次产生3个字节, 例如: 08 12 34, 

![在这里插入图片描述](https://img-blog.csdnimg.cn/2020070118450930.png)

- 上图是第一个字节:
	- 高4位表示移动方向, 第6位和第7位永远是0, 第4 和 第5 位表示xy方向: 0表示正向, 1 表示负向. 
	- 低4位表示击键,其中第3位永远是1, 所以 1001(0x9) 表示左键, 1010(0xA) 表示右键, 1100(0xC) 表示中键
- 12 表示左移或者右移的长度, 如果是负向, 则是补码方式表示, 即Fx.
- 34 表示上移或者下移的长度, 如果是负向, 则是补码方式表示, 即Fx.


```c
	// 下面开始接收鼠标的三个字节(三次中断)
	if (mdec->phase == 1) {
		/* マウスの1バイト目を待っている段階 */
		if ((dat & 0xc8) == 0x08) {  // 确保第一个字节是00xx1xxx的格式.
			/* 正しい1バイト目だった */
			mdec->buf[0] = dat;
			mdec->phase = 2;
		}
		return 0;
	}
	if (mdec->phase == 2) {
		/* マウスの2バイト目を待っている段階 */
		mdec->buf[1] = dat;
		mdec->phase = 3;
		return 0;
	}
	if (mdec->phase == 3) {
		/* マウスの3バイト目を待っている段階 */
		mdec->buf[2] = dat; // 接收第3个字节
		mdec->phase = 1;  // 重置阶段
		mdec->btn = mdec->buf[0] & 0x07; // 取出来低3位
		mdec->x = mdec->buf[1];
		mdec->y = mdec->buf[2];
		if ((mdec->buf[0] & 0x10) != 0) { // 0x10 表示 x 方向是负数
			mdec->x |= 0xffffff00; // x 是8位且是个负数, 所以补齐剩下的24位(补码表示).
		}
		if ((mdec->buf[0] & 0x20) != 0) { // 0x20 表示 y 方向是负数
			mdec->y |= 0xffffff00; // y 是8位且是个负数, 所以补齐剩下的24位(补码表示).
		}
		mdec->y = - mdec->y; /* マウスではy方向の符号が画面と反対 */
		return 1;
	}
```

# asmhead.nas

```asm
; haribote-os boot asm
; TAB=4

BOTPAK	EQU		0x00280000		; bootpackのロード先
DSKCAC	EQU		0x00100000		; ディスクキャッシュの場所
DSKCAC0	EQU		0x00008000		; ディスクキャッシュの場所（リアルモード）

; BOOT_INFO関係
CYLS	EQU		0x0ff0			; ブートセクタが設定する
LEDS	EQU		0x0ff1
VMODE	EQU		0x0ff2			; 色数に関する情報。何ビットカラーか？
SCRNX	EQU		0x0ff4			; 解像度のX
SCRNY	EQU		0x0ff6			; 解像度のY
VRAM	EQU		0x0ff8			; グラフィックバッファの開始番地

		ORG		0xc200			; このプログラムがどこに読み込まれるのか

; 画面モードを設定

		MOV		AL,0x13			; VGAグラフィックス、320x200x8bitカラー
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8	; 画面モードをメモする（C言語が参照する）
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000

; キーボードのLED状態をBIOSに教えてもらう

		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PIC的中断全部关闭
;	AT兼容设备的规格中，如果要初始化PIC，
;	必须在CLI前做，否则偶尔会挂起来
;	随后初花PIC

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; 有些机种如果连续执行OUT指令，就会不太顺利，
		OUT		0xa1,AL

		CLI						; 禁止CPU级别的中断

; CPUから1MB以上のメモリにアクセスできるように、A20GATEを設定
; 这次输出Oxdf所要完成的功能，是让A20GATE信号线变成ON的状态。这条信号线的作用是什么呢?它能使内存的1MB以上的部分变成可使用状态。最初出现电脑的时候，CPU只有16位模式，所以内存最大也只有1MB。后来CPU变聪明了，可以使用很大的内存了。但为了兼容旧版的操作系统，在执行激活指令之前，电路被限制为只能使用1MB内存。和鼠标的情况很类似哟。A20GATE信号线正是用来使这个电路停止从而让所有内存都可以使用的东西。

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL ; 让键盘电路执行 0xd1 指令
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL  ;  告诉键盘电路, 0xd1指令的参数是 0xdf
		CALL	waitkbdout

; プロテクトモード移行

[INSTRSET "i486p"]				; 使用 486 命令	

		LGDT	[GDTR0]			; 暫定GDTを設定
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit31を0にする（禁止分页）
		OR		EAX,0x00000001	; bit0を1にする（切换到保护模式）
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  可读写的段 32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpackの転送

		MOV		ESI,bootpack	; 転送元
		MOV		EDI,BOTPAK		; 転送先
		MOV		ECX,512*1024/4
		CALL	memcpy

; ついでにディスクデータも本来の位置へ転送

; まずはブートセクタから

		MOV		ESI,0x7c00		; 転送元
		MOV		EDI,DSKCAC		; 転送先
		MOV		ECX,512/4
		CALL	memcpy

; 残り全部

		MOV		ESI,DSKCAC0+512	; 転送元
		MOV		EDI,DSKCAC+512	; 転送先
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; シリンダ数からバイト数/4に変換
		SUB		ECX,512/4		; IPLの分だけ差し引く
		CALL	memcpy

; asmheadでしなければいけないことは全部し終わったので、
;	あとはbootpackに任せる

; bootpackの起動

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; 転送するべきものがない
		MOV		ESI,[EBX+20]	; 転送元
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; 転送先
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; スタック初期値
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; ANDの結果が0でなければwaitkbdoutへ
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; 引き算した結果が0でなければmemcpyへ
		RET
; memcpyはアドレスサイズプリフィクスを入れ忘れなければ、ストリング命令でも書ける

		ALIGNB	16
GDT0:
		RESB	8				; ヌルセレクタ
		DW		0xffff,0x0000,0x9200,0x00cf	; 読み書き可能セグメント32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; 実行可能セグメント32bit（bootpack用）

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:

```

# 参考

https://blog.csdn.net/weixin_42707324/article/details/107069180